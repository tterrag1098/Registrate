buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.+', changing: true
    }
}
plugins {
    id "io.freefair.lombok" version "4.1.5"
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'org.ajoberstar.grgit' version '3.1.1'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'

boolean dev = System.getenv('RELEASE') == null || System.getenv('RELEASE').equals('false')
def getVersion = {
    def raw = grgit.describe(longDescr: true, tags:true)
    def desc = (raw == null ? '0.0-0-unknown' : grgit.describe(longDescr: true, tags:true)).split('-') as List
    def hash = desc.remove(desc.size() - 1).substring(1)
    def offset = desc.remove(desc.size() - 1)
    return project.mod_version.replace('x', offset) + (System.getenv("BUILD_NUMBER") == null ? "+${hash}" : '')
}

version = "MC${minecraft_version}-${getVersion()}"
group = "com.tterrag.registrate"
archivesBaseName = "Registrate"

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

minecraft {
    mappings channel: 'official', version: minecraft_version

    runs {
        client {
            taskName 'testmod_client'
            
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                testmod {
                    source sourceSets.test
                    source sourceSets.main
                }
            }
        }
        server {
            taskName 'testmod_server'
            
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                testmod {
                    source sourceSets.test
                    source sourceSets.main
                }
            }
        }
        data {
            taskName 'testmod_data'
        
            workingDirectory project.file('run')
            
            environment 'target', 'fmluserdevdata'

            property 'forge.logging.markers', 'REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                testmod {
                    source sourceSets.test
                    source sourceSets.main
                }
            }
            
            args '--mod', 'testmod', '--all', '--output', rootProject.file('src/generated/resources/'), '--existing', rootProject.file('src/test/resources') 
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    testImplementation     "org.junit.jupiter:junit-jupiter-api:5.2.0"
    testRuntimeOnly        "org.junit.jupiter:junit-jupiter-engine:5.2.0"
}

lombok {
    version = "1.18.10"
    config['lombok.addJavaxGeneratedAnnotation'] = 'true'
}

delombok {
    format = [
        'suppressWarnings': 'skip',
        'javaLangAsFQN': 'skip'
    ]
}

jar {
    exclude('assets/**', 'data/**', '.cache/**') // Exclude generated assets, can't find a better way to do this :(
    manifest {
        attributes(
            'FMLModType': 'LIBRARY'
        )
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.delombokTask
    exclude 'com.tterrag.registrate.test.*'
}

javadoc {
    source = delombok
    options.addStringOption('tag', 'apiNote:a:API Note:')
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourceJar
        }
    }

    repositories {
        if (project.hasProperty('mavendir')) {
            maven { url mavendir }
        }
    }
}

String getChangelogText() {
    def changelogFile = file('changelog.txt')
    String str = ''
    int lineCount = 0
    boolean done = false
    changelogFile.eachLine {
        if (done || it == null) {
            return
        }
        if (it.size() > 1) {
            def temp = it
            if (lineCount == 0) {
                temp = "Chisel ${version}"
                temp = "<h2>$temp</h2>"
            } else if (it.startsWith('-')) {
                temp = "&nbsp;&nbsp;&nbsp;$temp"
                temp = temp.replaceAll("(\\S+\\/\\S+)#([0-9]+)\\b", "<a href=\"https://github.com/\$1/issues/\$2\">\$0</a>")
                temp = temp.replaceAll("#([0-9]+)\\b(?!<\\/a>)", "<a href=\"https://github.com/$github_project/issues/\$1\">\$0</a>")
            } else {
                temp = "<h4>$temp</h4>"
            }
            str += "$temp<br/>"
            lineCount++
            return
        } else {
            done = true
        }
    }
    return str
}
